<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_MCMachineControl" Id="{03d9ff00-a300-4d9d-a349-75e3318a4c90}" SpecialFunc="None">
    <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		04.11.2025		TC 4024.66
	Główny FB zarządzający pracą maszyny
*)
FUNCTION_BLOCK FB_MCMachineControl EXTENDS FB_BaseTopElement IMPLEMENTS ITF_MCMachineControl, ITF_MCMachineControlConfigure, ITF_MCMachineControl_HMI_Init
VAR
	(*	Inicjalizacja		*)
	bInitializeCycle			: BOOL;
	bInitDone					: BOOL;
	(*	Konfiguracja		*)
	itfMachineConfigure			: ITF_MCMachineConfigure;		//moduł konfiguracji
	bEnableHMIService			: BOOL;
	(*	TopElementy systemu	*)
	arrTopElements					: ARRAY [1..GVL_BaseConstans.MAX_NUMBER_OF_TOP_ELEMENT] OF ITF_BaseTopElement;
	uiNumberOfAddedTopElement		: UINT := 0;//przy dodawaniu element 0 to system 
	uiNumberOfAllAddedElements		: UINT;		//liczba elementow które są na maszynie 
	(*	Master komunikatora dla top elementów maszyny	*)
	fbMasterCommunicator			: FB_BaseCommunicator;
	(*	Zarządzenia stanami i trybami pracy	*)
	itfSaMController				: ITF_SaMController;
	(*	Stany i Tryby	*)
	arrRequestChangeState			: ARRAY [1..GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_STATES] OF ITF_MCRequiredChange;
	arrRequestChangeMode			: ARRAY [1..GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_MODES] OF ITF_MCRequiredChange;
	(*	Tablica informacyjna czy praca w stanie aktywnym zakończona	*)
	arrTopElementWorkFinishedInState	: ARRAY [1..GVL_SaMConstans.NUMBER_OF_STATES] OF BOOL;
	(*	Receptury	*)
	itfRecipe						: ITF_BaseRecipe;
	bWorkWithRecipe					: BOOL;
END_VAR
VAR PERSISTENT
	ltWorkTime						: LTIME;				//czas pracy w danym stanie - trybie
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF (NOT THIS^.bInitializeCycle) THEN
	THIS^.bInitializeCycle		:= TRUE;
	THIS^.bInitDone				:= THIS^.InitializeCycle ();
ELSIF (THIS^.bInitDone) THEN
	THIS^.ExecuteCycle ();
END_IF
]]></ST>
    </Implementation>
    <Folder Name="ITF_MachineControlConfigure" Id="{973d3ff0-85f0-4bae-9395-cc62a4917833}" />
    <Folder Name="ITF_MC_HMI_Connector" Id="{cacfec9c-669f-01d9-2025-b7ec347d32d7}" />
    <Folder Name="WiazanieModulow" Id="{3d8e3574-09c0-0f20-0429-cf1b0f8aec79}" />
    <Method Name="AddCreateTemplateFromConfiguration" Id="{dbe46cf0-9749-09d0-34d7-5296a504556d}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		26.11.2025		TC 4024.66
	Dodawanie szablonu receptury
*)
METHOD PUBLIC AddCreateTemplateFromConfiguration : BOOL
VAR_INPUT
	fbCreateTemplate			: ITF_BaseCreateTemplateOfRecipe;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.itfRecipe.AddITFToCreateTemplate (fbToCtreateTemplate := fbCreateTemplate);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddElementFromConfiguration" Id="{7dfdd75b-9905-0acd-138c-819c9dbddb2a}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Dodawanie elementów wywoływane z konfiguracji 
*)
METHOD PUBLIC AddElementFromConfiguration : BOOL
VAR_INPUT
	itfElements		: ITF_BaseTopElement;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (THIS^.uiNumberOfAddedTopElement + 1 <= GVL_BaseConstans.MAX_NUMBER_OF_TOP_ELEMENT) THEN
	THIS^.uiNumberOfAddedTopElement := THIS^.uiNumberOfAddedTopElement + 1;
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement] := itfElements;

	AddElementFromConfiguration := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddHMIManagerFromConfiguration" Id="{7cfeaa08-7b5d-0aee-0b40-25f6d8c78dfc}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Podpięcie menadżera HMI wywoływane z konfiguracji
*)
METHOD AddHMIManagerFromConfiguration : BOOL
VAR_INPUT
	fbHMIManager			: ITF_BaseGroupElement;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (THIS^.bEnableHMIService) THEN
	THIS^.AddControlElement (itfControlElement := fbHMIManager);
	
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddMachineCotrolToYourSelfFromConfiguration" Id="{dace522a-7d51-077e-3a49-9a0de96e23d6}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Dodanie samego sibie do obsługi w MachineControl
*)
METHOD PUBLIC AddMachineCotrolToYourSelfFromConfiguration : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.uiNumberOfAddedTopElement := THIS^.uiNumberOfAddedTopElement + 1;
THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement]	:= THIS^;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddModulConfigure" Id="{e05e86ab-e65a-0460-123e-9f866e36dccc}" FolderPath="WiazanieModulow\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		29.10.2025		TC 4024.66
	Dodawanie intefejsu konfiguracji
*)
METHOD PUBLIC AddModulConfigure : BOOL
VAR_INPUT
	fbMachineConfigure			: ITF_MCMachineConfigure;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.itfMachineConfigure		:= fbMachineConfigure;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddModulRecipeFromConfiguration" Id="{ac982117-770b-0933-3db6-4e07044fcdd7}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		26.11.2025		TC 4024.66
	Dodanie obsługi receptur
*)
METHOD PUBLIC AddModulRecipeFromConfiguration : BOOL
VAR_INPUT
	itfRecipe			: ITF_BaseRecipe;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.itfRecipe := itfRecipe;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddModulSaM" Id="{fdfb6f32-ad29-05b9-1f5f-f81a0f2962ac}" FolderPath="WiazanieModulow\">
      <Declaration><![CDATA[METHOD PUBLIC AddModulSaM : BOOL
VAR_INPUT
	fbSaMController			: ITF_SaMController;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.itfSaMController		:= fbSaMController;
THIS^.AddControlElement (itfControlElement	:= fbSaMController);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddSaMCotrollerToMachineControlFromConfiguration" Id="{c3944397-3e49-0760-2f0b-d3104ae7ce2d}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Dodanie modułu sterowania stanami i trybami do machinecontrol z konfiguracji
*)
METHOD PUBLIC AddSaMCotrollerToMachineControlFromConfiguration : BOOL
VAR_INPUT
	itfSaMController			: ITF_SaMController;
END_VAR
VAR
	iNumberOfTopElement			: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.AddControlElement (itfControlElement := itfSaMController);
THIS^.itfSaMController := itfSaMController;
//FOR iNumberOfTopElement := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
//	THIS^.arrTopElements [iNumberOfTopElement].LinkSaM (
//		fbSAM := itfSaMController
//	);
//END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddSaMToTopElementFromConfiguration" Id="{9e70fe67-ea23-0901-262c-47ed864679da}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[METHOD PUBLIC AddSaMToTopElementFromConfiguration
VAR_INPUT
	itfSaMController			: ITF_SaMController;
END_VAR

VAR
	iNumberOfTopElement: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iNumberOfTopElement := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.arrTopElements [iNumberOfTopElement].LinkSaM (
		fbSAM := itfSaMController
	);
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteCycle" Id="{7066db40-c8ea-4d9b-9b50-7a0d826fe39e}">
      <Declaration><![CDATA[METHOD PRIVATE ExecuteCycle
VAR_INST
	tonInterval	: TON;
END_VAR
VAR
	iI			: INT;
	tInterval	: TIME := T#100MS;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[	(*	Zarządzanie Stanami i Trybami	*)

THIS^.ExecuteCycle_SaM ();

	(*	Obsługa czasu *)

tonInterval (IN := NOT tonInterval.Q, PT := tInterval);
IF (tonInterval.Q) THEN
	THIS^.itfSaMController.IncrementTimeStateAndMode (tInterval := tInterval);
	THIS^.ltWorkTime := THIS^.ltWorkTime + TO_LTIME (tInterval);
END_IF

	(*	Wywołanie metod obsługiwanych co cykl dla każdego elementu	*)

FOR iI := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.arrTopElements [iI].ExecuteEveryCycle_Test ();
	THIS^.arrTopElements [iI].ExecuteEveryCycle_Operation ();
	THIS^.arrTopElements [iI].ExecuteEveryCycle_Status ();
	THIS^.arrTopElements [iI].ExecuteEveryCycle_Error ();
	
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteCycle_SaM" Id="{111ca252-3465-079b-3f8c-a42b1dc9a298}">
      <Declaration><![CDATA[METHOD PRIVATE ExecuteCycle_SaM
VAR
	iI					: INT;
	bBlockade			: BOOL;	//polecenie zmiany trybu i nie sprawdzaj czy jest polecenie zmiany stanu
	bTypeStateActive		: BOOL;
	bTopElementStateWorkCompleted	: BOOL;	//praca w trybach aktywnych zakończona
	
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
	(*	Zarządzanie stanami i trybami pracy 	*)

IF (NOT THIS^.itfSaMController.IsChangeModeBusy () AND NOT THIS^.itfSaMController.IsChangeStateBusy ()) THEN	
	(*	Testowanie czy jest rządzanie zmiany trybu	*)
	FOR iI := 1 TO GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_MODES DO 
		IF (THIS^.arrRequestChangeMode [iI] <> 0) THEN
			IF (THIS^.arrRequestChangeMode [iI].Testing ()) THEN
				THIS^.itfSaMController.Execute (iNumberOfCommand := iI, eChangeCategory := E_SaMChangeCategory.eMode);
				bBlockade		:= TRUE;
				EXIT;
			END_IF
		END_IF
	END_FOR	
	
	(*	Testowanie czy jest rządanie zmiany stany	*)
	IF (NOT bBlockade) THEN
		IF (THIS^.itfSaMController.GetMode () <> E_BaseMachineMode.NotAllowed AND
			THIS^.itfSaMController.GetState () = E_BaseMachineState.Undefined) THEN
			
			THIS^.itfSaMController.Execute (
				iNumberOfCommand := E_SaMCommandChangeState.Abort,
				eChangeCategory := E_SaMChangeCategory.eState
			);
			
		ELSE
			
			FOR iI := 1 TO GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_STATES DO 
				IF (THIS^.arrRequestChangeState [iI] <> 0) THEN
					IF (THIS^.arrRequestChangeState [iI].Testing ()) THEN
						THIS^.itfSaMController.Execute (
							iNumberOfCommand := iI,
							eChangeCategory := E_SaMChangeCategory.eState
						);
						EXIT;
					END_IF
				END_IF
			END_FOR
			
		END_IF
	END_IF

END_IF

	(*	Obsługa stanów 		*)
//informacja czy jesteśny w stanie aktywnym	
bTypeStateActive	 := SEL (THIS^.itfSaMController.GetState () <> E_BaseMachineState.Undefined,FALSE,THIS^.itfSaMController.IsStateTypeActive ());
IF (THIS^.itfSaMController.IsChangeModeDone () AND THIS^.itfSaMController.IsChangeStateDone ()) THEN
	bTopElementStateWorkCompleted := bTypeStateActive;
	IF (THIS^.itfSaMController.GetState () <> E_BaseMachineState.Undefined) THEN
		FOR iI := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
			IF ( (bTypeStateActive AND NOT THIS^.arrTopElementWorkFinishedInState [iI]) OR NOT bTypeStateActive) THEN
				THIS^.arrTopElementWorkFinishedInState [iI]	:= THIS^.itfSaMController.ExecuteTopElementOperationsForCurrentState (itfTopElement := THIS^.arrTopElements [iI]);
				bTopElementStateWorkCompleted			:= bTopElementStateWorkCompleted AND THIS^.arrTopElementWorkFinishedInState [iI];
			END_IF
		END_FOR
	END_IF
END_IF
(*	OPeracja zakonczona wyczysc tablice dla kolejnej zmiany stanow i konczenia pracy	*)
IF (bTopElementStateWorkCompleted) THEN
	MEMSET (
		destAddr							:= ADR (THIS^.arrTopElementWorkFinishedInState),
		fillByte							:= 0,
		n									:= SIZEOF (THIS^.arrTopElementWorkFinishedInState)
	);
	(*	Zmiana stanu na następny wyjście ze stanu aktywnego	*)
	THIS^.itfSaMController.ExitActivState ();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteFirstCycle" Id="{edf70b77-ba2f-0213-0d5c-b6184a3aaef1}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Wywołanie pierwszego przejścia u wszystkich podzepsołów
*)
METHOD PUBLIC ExecuteFirstCycle : BOOL
VAR_INPUT
END_VAR

VAR
	iNumberOfTopElement: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iNumberOfTopElement := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.arrTopElements [iNumberOfTopElement].FirstCycle ();
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetActiveMode" Id="{8082209e-2a25-0358-1ba6-8d6be4a4b203}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		25.11.2025		TC 4024.66
	Zwraca aktualny tryb
*)
METHOD PUBLIC GetActiveMode : E_BaseMachineMode
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetActiveMode := THIS^.itfSaMController.GetMode ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetActiveModeName" Id="{38a487c5-d808-014b-0d92-de101516c096}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[METHOD PUBLIC GetActiveModeName : ST_BaseLabel
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetActiveModeName := THIS^.itfSaMController.GetActiveModeName ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetActiveState" Id="{e5b5549c-336e-0574-2454-8e12e9984118}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		25.11.2025		TC 4024.66
	Zwraca aktualny stan
*)
METHOD PUBLIC GetActiveState : E_BaseMachineState
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetActiveState := THIS^.itfSaMController.GetState ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetActiveStateName" Id="{970ccbf0-5229-0375-0f13-aad6fb5dbacd}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[METHOD PUBLIC GetActiveStateName : ST_BaseLabel
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetActiveStateName := THIS^.itfSaMController.GetActiveStateName ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetCommunicatorITF" Id="{d2d430b9-5eb5-0584-25ef-418eb079b38b}">
      <Declaration><![CDATA[METHOD PUBLIC GetCommunicatorITF : ITF_BaseCommunicator
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetCommunicatorITF := THIS^.fbMasterCommunicator;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetElementNamesWithParameter" Id="{fc756e7e-b3c2-0fc1-3313-c3250c36a609}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		17.12.2025		TC 4024.66
	Pobiera listę elementów które posiadaja parametr i 
	przypisuje ja do zadanej struktury i zwraca true jak się uda
*)
METHOD GetElementNamesWithParameter : BOOL
VAR_INPUT
	stDane			: REFERENCE TO ST_MCElementListWithParameter;
END_VAR

VAR
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iI := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	;
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetModeName" Id="{d7dcb36a-81d4-015e-3c2d-4c15b356ceff}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[METHOD PUBLIC GetModeName : ST_BaseLabel
VAR_INPUT
	iNumber		: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetModeName := THIS^.itfSaMController.GetModeName (iNumber := iNumber);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetStateName" Id="{e5e4f5c5-63ee-0a0b-162a-2811dc8f9449}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[METHOD PUBLIC GetStateName : ST_BaseLabel
VAR_INPUT
	iNumber		: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetStateName := THIS^.itfSaMController.GetStateName (iNumber := iNumber);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetStateTimeForCurrentMode" Id="{2745c23a-6f63-0b92-39bd-64a014b88b46}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[METHOD GetStateTimeForCurrentMode : LTIME
VAR_INPUT
	iNumber			: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetStateTimeForCurrentMode := THIS^.itfSaMController. GetTimeStateForCurrentMode (iNumber := iNumber);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetStateTimeForMode" Id="{f5f50ad8-84a2-06c3-1498-61fa7938a5ef}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[METHOD GetStateTimeForMode : LTIME
VAR_INPUT
	iMode			: INT;
	iState			: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetStateTimeForMode := THIS^.itfSaMController.GetTimeStateForMode (iMode := iMode, iState := iState);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetTimeWorkForMode" Id="{4df6e39b-ac87-01ef-3b3e-7ad9f0d9a78d}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		25.11.2025		TC 4024.66
	Zrwaca wartośc pracy w trybie
*)
METHOD PUBLIC GetTimeWorkForMode : LTIME
VAR_INPUT
	iMode 	: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetTimeWorkForMode := THIS^.itfSaMController.GetTimeWorkForMode (iMode := iMode);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetTotalWorkTime" Id="{5386dcfb-16ba-0ebb-3c3f-abf0c21408bc}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		25.11.2025		TC 4024.66
	Zwraca całkowity czas pracy maszyny
*)
METHOD PUBLIC GetTotalWorkTime : LTIME
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetTotalWorkTime := THIS^.ltWorkTime;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="InitializeCycle" Id="{f127c58d-f380-4c5b-91fc-37aed1ddc70f}">
      <Declaration><![CDATA[METHOD PRIVATE InitializeCycle : BOOL
VAR
	bResult					: BOOL;
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bResult				:= TRUE;

bResult				:= bResult AND THIS^.itfMachineConfigure.Configure (fbMachineControl := THIS^);

	(*	Utworzenie szablownów	*)
IF (THIS^.itfRecipe.GetNumberOfTemplate () = 0 OR 
	THIS^.itfRecipe.GetNumberOfCreatingRecipe () <> THIS^.itfRecipe.GetNumberOfTemplate ()
	) THEN
	THIS^.itfRecipe.CreateTemplates ();
	THIS^.bWorkWithRecipe := TRUE;
END_IF

InitializeCycle		:= bResult;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsChangeModeDone" Id="{b9b5dec4-9849-0216-0284-2c1ce24b9762}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		25.11.2025		TC 4024.66
	Zwraca czy zmiana trybu sie skoćńzyła
*)
METHOD PUBLIC IsChangeModeDone : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsChangeModeDone := THIS^.itfSaMController.IsChangeModeDone ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsInitDone" Id="{4528eef7-0f59-0f1a-2d71-aaa809d5e525}">
      <Declaration><![CDATA[METHOD PUBLIC IsInitDone : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsInitDone		:= THIS^.bInitDone;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsModeEnabled" Id="{739e0b93-d97b-0854-3ca6-e50048c85998}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		25.11.2025		TC 4024.66
	Zwraca czy tryb o podanym numerze jest włączony
*)
METHOD IsModeEnabled : BOOL
VAR_INPUT
	iMode 	: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsModeEnabled := THIS^.itfSaMController.IsModeEnabled (iMode := iMode);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsStateActiveInCurrentMode" Id="{9055241c-5212-0274-131d-f8d6aba0eb69}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		24.11.2025		TC 4024.66
	Informacja czy dany stan jest aktywny w aktualnym trybie trybie
*)
METHOD PUBLIC IsStateActiveInCurrentMode : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsStateActiveInCurrentMode := THIS^.itfSaMController.IsStateInModeEnabled (eStateIn := THIS^.itfSaMController.GetState ());
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsStateIsActual" Id="{8e6ab011-08b1-0a9e-2197-98c13c53919e}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		25.11.2025		TC 4024.66
	Informacja czy zadany stan jest stanem aktualnym
*)
METHOD PUBLIC IsStateIsActual : BOOL
VAR_INPUT
	iTestState	: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsStateIsActual := THIS^.itfSaMController.IsStateIsActual (iTestState := iTestState);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="LinkCommonDataToAddedElementsFromConfiguration" Id="{694a103f-41a5-0324-38e3-12e026a9da84}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Zlinkowanie danych wspólnych do dodanych elementów wywoływane z konfiguracji
*)
METHOD PUBLIC LinkCommonDataToAddedElementsFromConfiguration : BOOL
VAR_INPUT
	stCommonData		: REFERENCE TO ST_BaseCommonData;
END_VAR

VAR
	iNumberOfTopElement: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iNumberOfTopElement := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.arrTopElements [iNumberOfTopElement].LinkCommonData (
		stCommonData := stCommonData
	);
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="LinkMassangerToaddedTopElementsFromConfiguration" Id="{676d4ba8-fe87-04d9-35d0-6c4c9941f800}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Dodanie głównego komunikatora pomiędzy topelementami do topelementów wywolywane z konfiguracji
*)
METHOD PUBLIC LinkMassangerToaddedTopElementsFromConfiguration : BOOL
VAR_INPUT
	fbCommunicator		: ITF_BaseCommunicator;
END_VAR

VAR
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.itfCommunicator := fbCommunicator;

FOR iI := 2 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.arrTopElements [iI].LinkCommunicator (fbCommunicator := fbCommunicator);
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="LinkRequestButtonModeChangeFromConfiguration" Id="{49d3b965-a6b2-0957-1dcd-d8d0f3112b66}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		14.11.2025		TC 4024.66
	Podpięcie przyciskow zmiany stanu wywoływane z konfiguracji 
*)
METHOD LinkRequestButtonModeChangeFromConfiguration : BOOL
VAR_INPUT
	iNumberOfRequest			: INT;
	itfRequest					: ITF_MCRequiredChange;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrRequestChangeMode [iNumberOfRequest]	:= itfRequest;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="LinkRequestButtonStateChangeFromConfiguration" Id="{d4ccb592-970e-0ea2-15fa-e0a3407d8233}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		14.11.2025		TC 4024.66
	Podpięcie przyciskow zmiany stanu wywoływane z konfiguracji 
*)
METHOD LinkRequestButtonStateChangeFromConfiguration : BOOL
VAR_INPUT
	iNumberOfRequest			: INT;
	itfRequest					: ITF_MCRequiredChange;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrRequestChangeState [iNumberOfRequest]	:= itfRequest;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="LinkSafetyToaddedElementsFromConfiguration" Id="{421093b7-8353-0bdc-2224-ca9aae0b6f9c}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Podłączenie wsyzstkich zmiennych pochodzących od Programu safety wywoływane w konfiguratorze
*)
METHOD PUBLIC LinkSafetyToaddedElementsFromConfiguration : BOOL
VAR_INPUT
	flSS1				: ITF_BaseFlagGet;
	flSTO				: ITF_BaseFlagGet;
END_VAR

VAR
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.LinkSS1 (fbSS1 := flSS1);
THIS^.LinkSTO (fbSTO := flSTO);

FOR iI := 2 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.arrTopElements [iI].LinkSS1 (fbSS1 := flSS1);
	THIS^.arrTopElements [iI].LinkSTO (fbSTO := flSTO);
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetCmdChangeMode" Id="{cb62aa39-24fd-0d9e-02a7-0fc03dc37d4e}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[METHOD PUBLIC SetCmdChangeMode
VAR_INPUT
	eCmdNumber		: E_SaMCommandChangeMode;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrRequestChangeMode [eCmdNumber].SetRequiredChange (bIn := TRUE);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetCmdChangeState" Id="{aaedd08e-f16e-012f-2524-bc1bc6905704}" FolderPath="ITF_MC_HMI_Connector\">
      <Declaration><![CDATA[METHOD PUBLIC SetCmdChangeState
VAR_INPUT
	eCmdNumber		: E_SaMCommandChangeState;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrRequestChangeState [eCmdNumber].SetRequiredChange (bIn := TRUE);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetCodeToAddedElementsFromConfiguration" Id="{9c0e8717-2de6-01d7-053e-0f21ff1e2b0f}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Nadanie unikalnych kodów elementom w systemie wywołane z konfiguracji
*)
METHOD PUBLIC SetCodeToAddedElementsFromConfiguration : BOOL
VAR_INPUT
END_VAR

VAR
	iNumberOfTopElement: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iNumberOfTopElement := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.arrTopElements [iNumberOfTopElement].SetCode (
		sCode := '0.0.0.0.0', sAdd := TO_STRING (iNumberOfTopElement)
	);
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetEnableHMIServiceFromConfiguration" Id="{1b03fb6c-ff23-066d-3ec3-09ff078a4135}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Uruchomienie lub wyłączenie obsługi HMI jak jest false fb obsługi nie jest dodawane do MachineControl wywolywane z konfiguracji
*)
METHOD SetEnableHMIServiceFromConfiguration : BOOL
VAR_INPUT
	bValue		: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.bEnableHMIService := bValue;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetIDEventsToAddedElementsFromConfiguration" Id="{3e3b7f79-eb1f-0356-2fb4-03649ebeaacb}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Nadaje unikalne ID zgłoszeniom dodanym do elementów w sesyemie wywołane z konfiguracji
*)
METHOD PUBLIC SetIDEventsToAddedElementsFromConfiguration : BOOL
VAR_INPUT
END_VAR
VAR
	iNumberOfTopElement	: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iNumberOfTopElement := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.refstCommonData.itfLogger.SetNumerOfReportsIntheSystem (
			uiValue := THIS^.arrTopElements [iNumberOfTopElement].SetEventsIDs (
				uiNewID := THIS^.refstCommonData.itfLogger.GetNumerOfReportsIntheSystem ()
			)
	);
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetIDToAddedElementsFromConfiguration" Id="{02d9bff6-6a30-0c5f-1b06-10a6d4b0df72}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		13.11.2025		TC 4024.66
	Dodanie wszystkim elementom w systemie unikalnego ID
*)
METHOD PUBLIC SetIDToAddedElementsFromConfiguration : BOOL
VAR_INPUT
END_VAR
VAR
	iNumberOfTopElement		: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iNumberOfTopElement := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.uiNumberOfAllAddedElements := 
		THIS^.arrTopElements [iNumberOfTopElement].SetID (
			uiID := THIS^.uiNumberOfAllAddedElements + 1
		);
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetStartModeFromConfiguration" Id="{43529a63-c868-0bd4-2738-b9b5832c43ec}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		14.11.2025		TC 4024.66
	ustawienie trybu poczatkowego wywoływane z konfiguracji
*)
METHOD SetStartModeFromConfiguration : BOOL
VAR_INPUT
	eStartMode				: E_BaseMachineMode;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.itfSaMController.Execute (iNumberOfCommand := eStartMode, eChangeCategory := E_SaMChangeCategory.eMode);]]></ST>
      </Implementation>
    </Method>
    <Method Name="STARE_AddHMIManager" Id="{83eb5c4d-6a27-0a18-07a0-6bb5a84b3772}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[METHOD STARE_AddHMIManager : BOOL
VAR_INPUT
	itfHMIManager			: ITF_BaseGroupElement;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.AddControlElement (itfControlElement := itfHMIManager);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="STARE_AddTopElementFromConfiguration" Id="{100f011a-3941-0f6f-14c0-c4dea046c178}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		29.10.2025		TC 4024.66
	Metoda dodawania top elementu wywoływana z konfiguracji
*)
METHOD STARE_AddTopElementFromConfiguration : BOOL
VAR_INPUT
	fbTopElement		: ITF_BaseTopElement;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (THIS^.uiNumberOfAddedTopElement + 1 <= GVL_BaseConstans.MAX_NUMBER_OF_TOP_ELEMENT) THEN
	THIS^.uiNumberOfAddedTopElement							:= THIS^.uiNumberOfAddedTopElement + 1;
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement]	:= fbTopElement;
	
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkCommonData (stCommonData	:= THIS^.refstCommonData);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkSS1 (fbSS1	:= THIS^.itfSS1);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkSTO (fbSTO	:= THIS^.itfSTO);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkCommunicator (fbCommunicator	:= THIS^.fbMasterCommunicator);
	(*	Podpięcie systemu sterowania stanami i trybami	*)
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkSAM (fbSAM	:= THIS^.itfSaMController);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].FirstCycle ();
	THIS^.uiNumberOfAllAddedElements 						:= THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].SetID (uiID := THIS^.uiNumberOfAllAddedElements + 1);
	THIS^.refstCommonData.itfLogger.SetNumerOfReportsIntheSystem (
			uiValue := THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].SetEventsIDs (
				uiNewID := THIS^.refstCommonData.itfLogger.GetNumerOfReportsIntheSystem ()
			)
	);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].SetCode (sCode := '0.0.0.0.0', sAdd := TO_STRING (THIS^.uiNumberOfAddedTopElement));
	
	STARE_AddTopElementFromConfiguration							:= TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>