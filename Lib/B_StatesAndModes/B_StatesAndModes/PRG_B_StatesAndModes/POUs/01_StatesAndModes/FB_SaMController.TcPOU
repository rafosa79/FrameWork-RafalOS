<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_SaMController" Id="{4563e61a-16f1-0b7e-1e7a-1e3cce62ebf6}" SpecialFunc="None">
    <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		30.10.2025		TC 4024.66
	Główny FB do zarządzania stanami i trybami pracy
*)
FUNCTION_BLOCK FB_SaMController EXTENDS FB_BaseControlElement IMPLEMENTS ITF_SaMController
VAR
(*	Polecenia zmiany stany	*)	
	stCmdStates					: ST_CommandChangeState;
	arrcmdStates				: ARRAY [1..GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_STATES] OF ITF_CommandChange;
(*	Polecenia zmiany trybu	*)
	stCmdModes					: ST_CommandChangeMode;
	arrcmdModes					: ARRAY [1..GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_MODES] OF ITF_CommandChange;
(*	Stany	*)
	arrstStates					: ARRAY [0..GVL_SaMConstans.NUMBER_OF_MODES] OF ST_SaMElementStates;
(*	Tryby	*)
	stModes						: ST_SaMElementModes;
(*	Przypisane stany do trybów	*)
	arrStatesInModes			: ARRAY [0..GVL_SaMConstans.NUMBER_OF_MODES] OF ARRAY [0..GVL_SaMConstans.NUMBER_OF_STATES] OF ITF_SaMElement;
	arrModes					: ARRAY [0..GVL_SaMConstans.NUMBER_OF_MODES] OF ITF_SaMElement;
(*	Referencja do tablicy stanów aktywnego trybu	*)
	refarrStatesForActiveMode	: REFERENCE TO ARRAY [0..GVL_SaMConstans.NUMBER_OF_STATES] OF ITF_SaMElement;
(*	Jakie trybu są aktywne	*)
	arrModeEnable				: ARRAY [0..GVL_SaMConstans.NUMBER_OF_MODES] OF BOOL;
(*	Jakie stany są aktywne w trybie	*)
	arrStateEnableInMode		: ARRAY [0..GVL_SaMConstans.NUMBER_OF_MODES] OF ARRAY [0..GVL_SaMConstans.NUMBER_OF_STATES] OF BOOL;
(*	W jakich stanach można przejść do innego trybu	*)
	arrModeTransitionEnableInState	: ARRAY [0..GVL_SaMConstans.NUMBER_OF_MODES] OF ARRAY [0..GVL_SaMConstans.NUMBER_OF_STATES] OF BOOL;
(*	Polecenia zmiany	*)
	cmdChangeMode				: FB_BaseCommand;
	cmdChangeState				: FB_BaseCommand;
(*	Zmienne		*)
	eState						: E_BaseMachineState;	//aktualny stan
	eMode						: E_BaseMachineMode;	//aktualny tryb
	eNewState					: E_BaseMachineState;	//Nowy stan
	ePrevState					: E_BaseMachineState;	//poprzedni stan
	eNewMode					: E_BaseMachineMode;	//nowy tryb
	ePrevMode					: E_BaseMachineMode;	//poprzedni tryb
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Initialization" Id="{36e42ff2-7480-0d56-1afc-450089a48df2}" />
    <Folder Name="ITF_BaseComponent" Id="{a4eda1c2-347a-018f-2a19-55fde3b3c605}" />
    <Folder Name="ITF_SaMController" Id="{c8bddf21-ded7-0091-2800-8c3c9b4e262e}" />
    <Folder Name="ITF_SaMControllerCommandChange" Id="{dea3c85f-ea23-061e-2958-91b06b29c270}" />
    <Folder Name="ITF_SaMControllerConfigure" Id="{b9d762b5-64ba-0e23-0da8-02679c5e095d}" />
    <Method Name="ChangeModeCycle" Id="{414aeb19-c018-007c-1c3c-f514bbd0a2d0}">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		04.11.2025		TC 4024.66
	cykl zmiany trybu
*)
METHOD PRIVATE ChangeModeCycle : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE THIS^.cmdChangeMode.GetStep () OF
	0	:
		THIS^.arrModes [THIS^.eMode].SetActive (bIN := FALSE);
		THIS^.ePrevMode			:= THIS^.eMode;
		THIS^.eMode				:= THIS^.eNewMode;
		THIS^.arrModes [THIS^.eMode].SetActive (bIN := TRUE);
		THIS^.refarrStatesForActiveMode	REF= THIS^.arrStatesInModes [THIS^.eMode];
		THIS^.cmdChangeMode.SetStepAsFinal ();
END_CASE
THIS^.cmdChangeMode.TestingLastCycle ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ChangeModeTo" Id="{6b94e17f-b8f4-087f-0f6f-660d61de79ee}" FolderPath="ITF_SaMControllerCommandChange\">
      <Declaration><![CDATA[METHOD PUBLIC ChangeModeTo
VAR_INPUT
	eNewMode			: E_BaseMachineMode;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.eNewMode			:= eNewMode;
THIS^.cmdChangeMode.Execute ();]]></ST>
      </Implementation>
    </Method>
    <Method Name="ChangeStateCycle" Id="{ba985810-e640-0078-3ef3-fe030dd2b4f0}">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		04.11.2025		TC 4024.66
	cykl zmiany stanu
*)
METHOD PRIVATE ChangeStateCycle : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE THIS^.cmdChangeState.GetStep () OF
	0	:
		THIS^.refarrStatesForActiveMode [THIS^.eState].SetActive (bIN := FALSE);
		THIS^.ePrevState		:= THIS^.eState;
		THIS^.eState			:= THIS^.eNewState;
		THIS^.refarrStatesForActiveMode [THIS^.eState].SetActive (bIN := TRUE);
		THIS^.cmdChangeState.SetStepAsFinal ();
END_CASE
THIS^.cmdChangeState.TestingLastCycle ();]]></ST>
      </Implementation>
    </Method>
    <Method Name="ChangeStateTo" Id="{628f54fb-c326-0184-1055-f36dc1e3eef6}" FolderPath="ITF_SaMControllerCommandChange\">
      <Declaration><![CDATA[METHOD PUBLIC ChangeStateTo
VAR_INPUT
	eNewState			: E_BaseMachineState;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.eNewState			:= eNewState;
THIS^.cmdChangeState.Execute ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute" Id="{608fbfab-efd0-0a43-132b-9ced413e3cf8}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		03.11.2025		TC 4024.66
	Obsługa zmiany stanów i trybów 
*)
METHOD Execute : BOOL
VAR_INPUT
	iNumberOfCommand		: INT;
	eChangeCategory			: E_SaMChangeCategory;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE eChangeCategory OF
	E_SaMChangeCategory.eState	:
		THIS^.arrcmdStates [iNumberOfCommand].Execute (iNumberOfCommand	:= iNumberOfCommand);
	E_SaMChangeCategory.eMode	:
		THIS^.arrcmdModes [iNumberOfCommand].Execute (iNumberOfCommand	:= iNumberOfCommand);
END_CASE
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteEveryCycle_Error" Id="{7d2915c0-b860-0ae5-1527-3f4383f1e352}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		27.03.2025		TC 4024.66
	Obsluga bledow sprawdzanie czy jakis blad sie nei pojawil i ustawienie stanu lub wylaczenie
*)
METHOD PUBLIC ExecuteEveryCycle_Error
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.ExecuteEveryCycle_Error ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteEveryCycle_Operation" Id="{fce1133f-4269-083a-3196-447fd3d305da}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		27.03.2025		TC 4024.66
	Wywolanie co cykl operacji 
*)
METHOD PUBLIC ExecuteEveryCycle_Operation
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.ExecuteEveryCycle_Operation ();

IF (THIS^.cmdChangeMode.OperationExecute ()) THEN
	THIS^.cmdChangeMode.Operation (bIN := THIS^.ChangeModeCycle ());
END_IF

IF (THIS^.cmdChangeState.OperationExecute ()) THEN
	THIS^.cmdChangeState.Operation (bIN := THIS^.ChangeStateCycle ());
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteEveryCycle_Status" Id="{efc19d29-865a-0cf3-3de7-0ea6cd45bbeb}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		27.03.2025		TC 4024.66
	Aktualizacja flag 
*)
METHOD PUBLIC ExecuteEveryCycle_Status
VAR_INST
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.ExecuteEveryCycle_Status ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteEveryCycle_Test" Id="{ece18da2-ea2a-0733-0204-177085361f4d}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		27.03.2025		TC 4024.66
	Testowanie czy nei pojawil sie jakis rozkaz i uruchomienie odpowiednich operacji
*)
METHOD PUBLIC ExecuteEveryCycle_Test
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.ExecuteEveryCycle_Test ();

THIS^.cmdChangeMode.Test ();
THIS^.cmdChangeState.Test ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteTopElementOperationsForCurrentState" Id="{9f312f84-4c60-084c-1618-92d105754996}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD ExecuteTopElementOperationsForCurrentState : BOOL
VAR_INPUT
	itfTopElement				: ITF_BaseSaMStateMethod;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ExecuteTopElementOperationsForCurrentState := THIS^.refarrStatesForActiveMode [THIS^.eState].InvokeStateForTopElement (itfTopElement := itfTopElement);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExitActivState" Id="{ef664fb1-428d-0779-28b7-c91802d9823c}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		04.11.2025		TC 4024.66
	Wywołanie odpowiednie metody tak by wyjśc z aktualnego stanu aktywnego 
*)
METHOD PUBLIC ExitActivState
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.ChangeStateTo (eNewState := THIS^.refarrStatesForActiveMode [THIS^.eState].GetNextState ());
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{8f55a5c3-a0eb-0deb-011c-23bf5df2ad6a}" FolderPath="Initialization\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.SetText (eLocation := E_BaseLocation.pl, wsDescription := "SaM Controller");
THIS^.SetText (eLocation := E_BaseLocation.en, wsDescription := "SaM Controller");
THIS^.SetText (eLocation := E_BaseLocation.de, wsDescription := "SaM Controller");
THIS^.SetName (stName := THIS^.GetTexts ());

THIS^.Initialization_AddStates ();
THIS^.Initialization_AddModes ();

THIS^.Initialization_AddCommandChangeState ();
THIS^.Initialization_AddCommandChangeModes ();

THIS^.Initialization_StateEnabledInMode ();
THIS^.Initialization_ModeTransitionEnabledInState ();
THIS^.Initialization_ModeEnabled ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetMode" Id="{df3fefc4-9266-079c-2077-340c16420786}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD PUBLIC GetMode : E_BaseMachineMode
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetMode			:= THIS^.eMode;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetState" Id="{e651e76f-e8a9-016f-279f-b41f48a21ac0}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD PUBLIC GetState : E_BaseMachineState
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetState			:= THIS^.eState;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialization_AddCommandChangeModes" Id="{1109a5fd-0b62-0ed8-0a3b-0f57bddc8c59}" FolderPath="Initialization\">
      <Declaration><![CDATA[METHOD PRIVATE Initialization_AddCommandChangeModes : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
THIS^.arrcmdModes [E_SaMCommandChangeMode.Production]	:= THIS^.stCmdModes.cmdChangeProduction;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdModes.cmdChangeProduction);
THIS^.stCmdModes.cmdChangeProduction.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdModes [E_SaMCommandChangeMode.Maintenance]	:= THIS^.stCmdModes.cmdChangeMaintenance;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdModes.cmdChangeMaintenance);
THIS^.stCmdModes.cmdChangeMaintenance.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdModes [E_SaMCommandChangeMode.Manual]		:= THIS^.stCmdModes.cmdChangeManual;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdModes.cmdChangeManual);
THIS^.stCmdModes.cmdChangeManual.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdModes [E_SaMCommandChangeMode.Washing]		:= THIS^.stCmdModes.cmdChangeWashing;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdModes.cmdChangeWashing);
THIS^.stCmdModes.cmdChangeWashing.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdModes [E_SaMCommandChangeMode.Diagnostisc]	:= THIS^.stCmdModes.cmdChangeDiagnostics;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdModes.cmdChangeDiagnostics);
THIS^.stCmdModes.cmdChangeDiagnostics.LinkSaMCotroller (itfSaMController	:= THIS^);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialization_AddCommandChangeState" Id="{7530a13b-678f-0aa6-14bb-d0e2275487dd}" FolderPath="Initialization\">
      <Declaration><![CDATA[METHOD PRIVATE Initialization_AddCommandChangeState : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrcmdStates [E_SaMCommandChangeState.Abort]	:= THIS^.stCmdStates.cmdChangeAbort;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeAbort);
THIS^.stCmdStates.cmdChangeAbort.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdStates [E_SaMCommandChangeState.Clear]	:= THIS^.stCmdStates.cmdChangeClear;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeClear);
THIS^.stCmdStates.cmdChangeClear.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdStates [E_SaMCommandChangeState.Reset]	:= THIS^.stCmdStates.cmdChangeReset;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeReset);
THIS^.stCmdStates.cmdChangeReset.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdStates [E_SaMCommandChangeState.Start]	:= THIS^.stCmdStates.cmdChangeStart;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeStart);
THIS^.stCmdStates.cmdChangeStart.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdStates [E_SaMCommandChangeState.Stop]	:= THIS^.stCmdStates.cmdChangeStop;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeStop);
THIS^.stCmdStates.cmdChangeStop.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdStates [E_SaMCommandChangeState.Hold]	:= THIS^.stCmdStates.cmdChangeHold;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeHold);
THIS^.stCmdStates.cmdChangeHold.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdStates [E_SaMCommandChangeState.Unhold]	:= THIS^.stCmdStates.cmdChangeUnhold;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeUnhold);
THIS^.stCmdStates.cmdChangeUnhold.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdStates [E_SaMCommandChangeState.Suspend]:= THIS^.stCmdStates.cmdChangeSuspend;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeSuspend);
THIS^.stCmdStates.cmdChangeSuspend.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdStates [E_SaMCommandChangeState.Unsuspend]	:= THIS^.stCmdStates.cmdChangeUnSuspend;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeUnSuspend);
THIS^.stCmdStates.cmdChangeUnSuspend.LinkSaMCotroller (itfSaMController	:= THIS^);

THIS^.arrcmdStates [E_SaMCommandChangeState.Complete]	:= THIS^.stCmdStates.cmdChangeComplete;
THIS^.AddComponent (itfComponent 	:= THIS^.stCmdStates.cmdChangeComplete);
THIS^.stCmdStates.cmdChangeComplete.LinkSaMCotroller (itfSaMController	:= THIS^);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialization_AddModes" Id="{2b512a92-51af-0b34-2a0b-9e312b0ddf05}" FolderPath="Initialization\">
      <Declaration><![CDATA[METHOD PRIVATE Initialization_AddModes : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrModes [E_BaseMachineMode.NotAllowed]	:= THIS^.stModes.fbUndefined;
THIS^.AddComponent (itfComponent	:= THIS^.stModes.fbUndefined);
THIS^.arrModes [E_BaseMachineMode.Production]	:= THIS^.stModes.fbProduction;
THIS^.AddComponent (itfComponent	:= THIS^.stModes.fbProduction);
THIS^.arrModes [E_BaseMachineMode.Maintenance]	:= THIS^.stModes.fbMaintenance;
THIS^.AddComponent (itfComponent	:= THIS^.stModes.fbMaintenance);
THIS^.arrModes [E_BaseMachineMode.Manual]	:= THIS^.stModes.fbManual;
THIS^.AddComponent (itfComponent	:= THIS^.stModes.fbManual);
THIS^.arrModes [E_BaseMachineMode.Washing]	:= THIS^.stModes.fbWashing;
THIS^.AddComponent (itfComponent	:= THIS^.stModes.fbWashing);
THIS^.arrModes [E_BaseMachineMode.Diagnostics]	:= THIS^.stModes.fbDiagnostics;
THIS^.AddComponent (itfComponent	:= THIS^.stModes.fbDiagnostics);



]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialization_AddStates" Id="{c7a2785d-ca07-0767-289b-70b582ac2ba0}" FolderPath="Initialization\">
      <Declaration><![CDATA[METHOD PRIVATE Initialization_AddStates : BOOL
VAR_INPUT
END_VAR

VAR
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iI := 0 TO GVL_SaMConstans.NUMBER_OF_MODES DO
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Undefined] 		:= THIS^.arrstStates [iI].fbUndefined;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Aborting] 		:= THIS^.arrstStates [iI].fbAborting;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Aborted] 		:= THIS^.arrstStates [iI].fbAborted;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Clearing] 		:= THIS^.arrstStates [iI].fbClearing;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Stopping] 		:= THIS^.arrstStates [iI].fbStopping;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Stopped] 		:= THIS^.arrstStates [iI].fbStopped;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Resetting] 		:= THIS^.arrstStates [iI].fbResetting;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Idle] 			:= THIS^.arrstStates [iI].fbIdle;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Starting] 		:= THIS^.arrstStates [iI].fbStarting;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Execute] 		:= THIS^.arrstStates [iI].fbExecute;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Suspending]	 	:= THIS^.arrstStates [iI].fbSuspending;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Suspended] 		:= THIS^.arrstStates [iI].fbSuspended;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Unsuspending]	:= THIS^.arrstStates [iI].fbUnsuspending;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Holding] 		:= THIS^.arrstStates [iI].fbHolding;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Held] 			:= THIS^.arrstStates [iI].fbHeld;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Unholding] 		:= THIS^.arrstStates [iI].fbUnholding;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Completing] 	:= THIS^.arrstStates [iI].fbCompleting;
	THIS^.arrStatesInModes [iI][E_BaseMachineState.Complete] 		:= THIS^.arrstStates [iI].fbComplete;
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbUndefined);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbAborting);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbAborted);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbClearing);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbStopping);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbStopped);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbResetting);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbIdle);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbStarting);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbExecute);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbSuspending);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbSuspended);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbUnsuspending);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbHolding);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbHeld);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbUnholding);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbCompleting);
	THIS^.AddComponent (itfComponent 	:= THIS^.arrstStates [iI].fbComplete);
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialization_CopeEnabledMode" Id="{9caf16aa-8e17-0188-0778-feb120a747c8}" FolderPath="Initialization\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		03.11.2025		TC 4024.66
	Przepisuje ustawiony stan Enebled z elementu i zapisuje go do tablisy trybów
*)
METHOD PRIVATE Initialization_CopeEnabledMode
VAR
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iI := 0 TO GVL_SaMConstans.NUMBER_OF_MODES DO
	IF (THIS^.arrModes [iI] <> 0) THEN
		THIS^.arrModeEnable [iI]	:= THIS^.arrModes [iI].IsEnabled ();
	END_IF
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialization_CopeEnabledStateToMode" Id="{f300e133-09be-0c0f-171e-7b6b40f26bca}" FolderPath="Initialization\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		03.11.2025		TC 4024.66
	Przepisuje ustawiony stan Enebled z elementu i zapisuje go do tablisy stanów w trybach
*)
METHOD PRIVATE Initialization_CopeEnabledStateToMode
VAR_INPUT
	eMode			: E_BaseMachineMode;
END_VAR

VAR
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR iI := 0 TO GVL_SaMConstans.NUMBER_OF_STATES DO
	IF (THIS^.arrStatesInModes [eMode][iI] <> 0) THEN
		THIS^.arrStateEnableInMode [eMode][iI]	:= THIS^.arrStatesInModes [eMode][iI].IsEnabled ();
	END_IF
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialization_ModeEnabled" Id="{5912d4c5-b4cd-06a3-0564-9ba3892b0b5a}" FolderPath="Initialization\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		03.11.2025		TC 4024.66
	Inicjalizacja jakie tryby są właczony
*)
METHOD PRIVATE Initialization_ModeEnabled
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.stModes.fbUndefined.SetEnabled (bIN	:= TRUE);
THIS^.stModes.fbProduction.SetEnabled (bIN	:= TRUE);
THIS^.stModes.fbMaintenance.SetEnabled (bIN	:= TRUE);
THIS^.stModes.fbManual.SetEnabled (bIN	:= TRUE);
THIS^.stModes.fbWashing.SetEnabled (bIN	:= TRUE);
THIS^.stModes.fbDiagnostics.SetEnabled (bIN	:= TRUE);
THIS^.Initialization_CopeEnabledMode ();

]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialization_ModeTransitionEnabledInState" Id="{7a55a83f-7f67-0de6-2883-392467d43383}" FolderPath="Initialization\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		03.11.2025		TC 4024.66
	Ustawienie w jakim stanie trybu mozemy zmieniać tryby
*)
METHOD PRIVATE Initialization_ModeTransitionEnabledInState
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.NotAllowed][E_BaseMachineState.Undefined]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Production][E_BaseMachineState.Undefined]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Maintenance][E_BaseMachineState.Undefined]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Manual][E_BaseMachineState.Undefined]			:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Washing][E_BaseMachineState.Undefined]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Diagnostics][E_BaseMachineState.Undefined]	:= TRUE;

THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.NotAllowed][E_BaseMachineState.Aborted]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Production][E_BaseMachineState.Aborted]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Maintenance][E_BaseMachineState.Aborted]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Manual][E_BaseMachineState.Aborted]			:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Washing][E_BaseMachineState.Aborted]			:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Diagnostics][E_BaseMachineState.Aborted]		:= TRUE;

THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.NotAllowed][E_BaseMachineState.Stopped]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Production][E_BaseMachineState.Stopped]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Maintenance][E_BaseMachineState.Stopped]		:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Manual][E_BaseMachineState.Stopped]			:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Washing][E_BaseMachineState.Stopped]			:= TRUE;
THIS^.arrModeTransitionEnableInState [E_BaseMachineMode.Diagnostics][E_BaseMachineState.Stopped]		:= TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialization_StateEnabledInMode" Id="{dc2a4a2f-5f9b-0acc-12ed-511725c24baa}" FolderPath="Initialization\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		03.11.2025		TC 4024.66
	Ustawienie jakie stany sa włączone w jakim stanie
*)
METHOD PRIVATE Initialization_StateEnabledInMode
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbUndefined.SetEnabled (bIN		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbAborting.SetEnabled (bIN		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbAborted.SetEnabled (bIN			:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbClearing.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbStopping.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbStopped.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbResetting.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbIdle.SetEnabled (bIN 			:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbStarting.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbExecute.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbSuspending.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbSuspended.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbUnsuspending.SetEnabled (bIN	:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbHolding.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbHeld.SetEnabled (bIN 			:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbUnholding.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbCompleting.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.NotAllowed].fbComplete.SetEnabled (bIN 		:= FALSE);
THIS^.Initialization_CopeEnabledStateToMode (eMode	:= E_BaseMachineMode.NotAllowed);


THIS^.arrstStates [E_BaseMachineMode.Production].fbUndefined.SetEnabled (bIN		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbAborting.SetEnabled (bIN		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbAborted.SetEnabled (bIN			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbClearing.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbStopping.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbStopped.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbResetting.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbIdle.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbStarting.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbExecute.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbSuspending.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbSuspended.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbUnsuspending.SetEnabled (bIN	:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbHolding.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbHeld.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbUnholding.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbCompleting.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Production].fbComplete.SetEnabled (bIN 		:= TRUE);
THIS^.Initialization_CopeEnabledStateToMode (eMode	:= E_BaseMachineMode.Production);

THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbUndefined.SetEnabled (bIN		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbAborting.SetEnabled (bIN			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbAborted.SetEnabled (bIN			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbClearing.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbStopping.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbStopped.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbResetting.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbIdle.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbStarting.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbExecute.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbSuspending.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbSuspended.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbUnsuspending.SetEnabled (bIN		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbHolding.SetEnabled (bIN 			:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbHeld.SetEnabled (bIN 			:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbUnholding.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbCompleting.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Maintenance].fbComplete.SetEnabled (bIN 		:= FALSE);
THIS^.Initialization_CopeEnabledStateToMode (eMode	:= E_BaseMachineMode.Maintenance);

THIS^.arrstStates [E_BaseMachineMode.Manual].fbUndefined.SetEnabled (bIN			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbAborting.SetEnabled (bIN			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbAborted.SetEnabled (bIN				:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbClearing.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbStopping.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbStopped.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbResetting.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbIdle.SetEnabled (bIN 				:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbStarting.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbExecute.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbSuspending.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbSuspended.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbUnsuspending.SetEnabled (bIN		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbHolding.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbHeld.SetEnabled (bIN 				:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbUnholding.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbCompleting.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Manual].fbComplete.SetEnabled (bIN 			:= TRUE);
THIS^.Initialization_CopeEnabledStateToMode (eMode	:= E_BaseMachineMode.Manual);

THIS^.arrstStates [E_BaseMachineMode.Washing].fbUndefined.SetEnabled (bIN			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbAborting.SetEnabled (bIN			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbAborted.SetEnabled (bIN			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbClearing.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbStopping.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbStopped.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbResetting.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbIdle.SetEnabled (bIN 				:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbStarting.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbExecute.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbSuspending.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbSuspended.SetEnabled (bIN 			:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbUnsuspending.SetEnabled (bIN		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbHolding.SetEnabled (bIN 			:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbHeld.SetEnabled (bIN 				:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbUnholding.SetEnabled (bIN 			:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbCompleting.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Washing].fbComplete.SetEnabled (bIN 			:= FALSE);
THIS^.Initialization_CopeEnabledStateToMode (eMode	:= E_BaseMachineMode.Washing);

THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbUndefined.SetEnabled (bIN		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbAborting.SetEnabled (bIN		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbAborted.SetEnabled (bIN		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbClearing.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbStopping.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbStopped.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbResetting.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbIdle.SetEnabled (bIN 			:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbStarting.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbExecute.SetEnabled (bIN 		:= TRUE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbSuspending.SetEnabled (bIN 	:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbSuspended.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbUnsuspending.SetEnabled (bIN	:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbHolding.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbHeld.SetEnabled (bIN 			:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbUnholding.SetEnabled (bIN 		:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbCompleting.SetEnabled (bIN 	:= FALSE);
THIS^.arrstStates [E_BaseMachineMode.Diagnostics].fbComplete.SetEnabled (bIN 		:= FALSE);
THIS^.Initialization_CopeEnabledStateToMode (eMode	:= E_BaseMachineMode.Diagnostics);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsChangeModeBusy" Id="{27f468b3-8c6e-0b25-0376-19a7957fed46}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD PUBLIC IsChangeModeBusy : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsChangeModeBusy		:= THIS^.cmdChangeMode.IsBusy ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsChangeModeDone" Id="{11624add-7cf7-064d-1293-942cd04c052f}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD PUBLIC IsChangeModeDone : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsChangeModeDone		:= THIS^.cmdChangeMode.IsDone ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsChangeModeStatus" Id="{4a89fd0d-9401-0402-15d0-686614f26d33}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD PUBLIC IsChangeModeStatus : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsChangeModeStatus		:= THIS^.cmdChangeMode.IsStatus ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsChangeStateBusy" Id="{8332f72f-1927-0e74-2e69-63470ebd1419}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD PUBLIC IsChangeStateBusy : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsChangeStateBusy		:= THIS^.cmdChangeState.IsBusy ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsChangeStateDone" Id="{1fa27fa4-f8fc-0233-01ff-b0111394a129}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD PUBLIC IsChangeStateDone : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsChangeStateDone		:= THIS^.cmdChangeState.IsDone ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsChangeStateStatus" Id="{f33bda38-41ba-0223-10fa-20c9fb40adbf}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD PUBLIC IsChangeStateStatus : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsChangeStateStatus		:= THIS^.cmdChangeState.IsStatus ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsModeEnebles" Id="{ba118139-1c1b-0f84-1e39-4e98f108464d}" FolderPath="ITF_SaMControllerCommandChange\">
      <Declaration><![CDATA[METHOD IsModeEnebles : BOOL
VAR_INPUT
	eMode			: E_BaseMachineMode;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsModeEnebles		:= THIS^.arrModeEnable [eMode];
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsPossibleExitActiveState" Id="{5a124e67-92fc-05a7-3c2a-49a85f4a9ae2}" FolderPath="ITF_SaMControllerCommandChange\">
      <Declaration><![CDATA[METHOD IsPossibleExitActiveState : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsPossibleExitActiveState	:= THIS^.IsPossibleExitState (eMode := THIS^.eMode);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsPossibleExitState" Id="{64bec622-8222-09ec-1977-c38277b5e18a}" FolderPath="ITF_SaMControllerCommandChange\">
      <Declaration><![CDATA[METHOD IsPossibleExitState : BOOL
VAR_INPUT
	eMode		: E_BaseMachineMode;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsPossibleExitState		:= THIS^.arrModeTransitionEnableInState [eMode][THIS^.eState];
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsStateInModeEnabled" Id="{0076f71f-25de-0166-149a-720af9605ad5}" FolderPath="ITF_SaMControllerCommandChange\">
      <Declaration><![CDATA[METHOD PUBLIC IsStateInModeEnabled : BOOL
VAR_INPUT
	eNewState			: E_BaseMachineState;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsStateInModeEnabled		:= THIS^.arrStateEnableInMode [THIS^.eMode][eNewState];
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsStateTypeActive" Id="{7bd25157-9596-071c-3b81-80cfb5064136}" FolderPath="ITF_SaMController\">
      <Declaration><![CDATA[METHOD PUBLIC IsStateTypeActive : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsStateTypeActive		:= THIS^.refarrStatesForActiveMode [THIS^.eState].IsStateTypeActive ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetModeEnabled" Id="{a6177f68-4488-047c-30fa-8a1ad845401a}" FolderPath="ITF_SaMControllerConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		03.11.2025		TC 4024.66
	Wlacza lub wylacza tryb pracy
*)
METHOD PUBLIC SetModeEnabled
VAR_INPUT
	iMode				: INT;	//numer trybu
	bValue				: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrModes [iMode].SetEnabled (bIN	:= bValue);
THIS^.Initialization_CopeEnabledMode ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetModeTransitionEnabledInState" Id="{8e1ba6f0-a320-0b3b-2227-dfce826a53ad}" FolderPath="ITF_SaMControllerConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		03.11.2025		TC 4024.66
	Ustawianie czy w danym stanie może nastąpić zmiana trybu
*)
METHOD PUBLIC SetModeTransitionEnabledInState 
VAR_INPUT
	iMode			: INT;	//tryb
	iState			: INT;	//stan
	bValue			: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrModeTransitionEnableInState [iMode][iState]	:= bValue;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetStateEnabledInMode" Id="{e6b6b323-bcad-0d41-3b46-111e4af3451a}" FolderPath="ITF_SaMControllerConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		03.11.2025		TC 4024.66
	Ustawia włączone stany w zadanym trybie
*)
METHOD PUBLIC SetStateEnabledInMode
VAR_INPUT
	iMode			: INT;	//tryb
	iState			: INT;	//stan
	bValue			: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrStateEnableInMode [iMode][iState] 	:= bValue;
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>