<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_BaseComponent" Id="{87e0f050-2d49-0545-1d2d-1d8cc43d61c2}" SpecialFunc="None">
    <Declaration><![CDATA[(*			
	RAFALOS
	Rafał Osajda		19.09.2025		TC 4024.66
	Komponent bazowy dla elementów prostych
*)
FUNCTION_BLOCK FB_BaseComponent EXTENDS FB_BaseLoggerElement IMPLEMENTS ITF_BaseComponent, ITF_BaseComponent_HMI_Connector
VAR
	cmdReset						: FB_BaseCommand;
	cmdParametersCreate				: FB_BaseCommand;
	cmdParametersRestoreToDefault	: FB_BaseCommand;
	
	
END_VAR
VAR PERSISTENT
	stParameters					: ST_Parameters;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="ITF_BaseComponent" Id="{b16f93e1-934e-0805-383f-d97d810c646e}" />
    <Folder Name="ITF_DecoratedElement" Id="{41521ef6-d598-047e-2c22-8e73616ee08c}" />
    <Folder Name="Parameter" Id="{51f1d98c-bdeb-0f9b-3843-7bd8ef234ec3}" />
    <Method Name="ExecuteEveryCycle_Error" Id="{d0f95f17-dd99-064c-15eb-b6a90cafd74d}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		27.03.2025		TC 4024.66
	Obsluga bledow sprawdzanie czy jakis blad sie nei pojawil i ustawienie stanu lub wylaczenie
*)
METHOD PUBLIC ExecuteEveryCycle_Error
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteEveryCycle_Operation" Id="{513159e8-2790-0493-315a-cd955c8d31c5}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		27.03.2025		TC 4024.66
	Wywolanie co cykl operacji 
*)
METHOD PUBLIC ExecuteEveryCycle_Operation
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (THIS^.cmdReset.OperationExecute ()) THEN
	THIS^.cmdReset.Operation (bIN		:= THIS^.ResetOperation ());
END_IF

IF (THIS^.cmdParametersCreate.OperationExecute ()) THEN
	THIS^.cmdParametersCreate.Operation (bIN := THIS^.ParameterCreateTable ());
END_IF

IF (THIS^.cmdParametersRestoreToDefault.OperationExecute ()) THEN
	THIS^.cmdParametersRestoreToDefault.Operation (bIN	:= THIS^.ParameterRestoreDefaultValueAll ());
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteEveryCycle_Status" Id="{4211d7fe-e3a3-005a-3d2b-874c421b8ff4}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		27.03.2025		TC 4024.66
	Aktualizacja flag 
*)
METHOD PUBLIC ExecuteEveryCycle_Status
VAR_INST
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteEveryCycle_Test" Id="{4131c775-8fd3-0b9a-02c8-9e9a0a682b52}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		27.03.2025		TC 4024.66
	Testowanie czy nei pojawil sie jakis rozkaz i uruchomienie odpowiednich operacji
*)
METHOD PUBLIC ExecuteEveryCycle_Test
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.cmdReset.Test ();
THIS^.cmdParametersCreate.Test ();
THIS^.cmdParametersRestoreToDefault.Test ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{f6b4fcc3-c3c0-0af2-28d1-79ccbb610da2}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.stIdentifier.eType			:= E_BaseElementType.eComponent;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FirstCycle" Id="{59d66408-2df1-04a5-240d-93e8399a3b7e}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		03.10.2025		TC 4024.66
	
*)
METHOD PUBLIC FirstCycle : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FirstCycle				:= SUPER^.FirstCycle ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetITFComponent" Id="{33e10c5b-120b-0f78-06cf-170e49fa6c9a}" FolderPath="ITF_DecoratedElement\">
      <Declaration><![CDATA[METHOD GetITFComponent : ITF_BaseComponent
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetITFComponent		:= THIS^;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetITFControlElement" Id="{9af90360-abec-083a-18a8-2f7d8868516c}" FolderPath="ITF_DecoratedElement\">
      <Declaration><![CDATA[METHOD GetITFControlElement : ITF_BaseControlElement
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetITFControlElement	:= null;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetITFGroupElement" Id="{aa64e9b4-f847-0695-3ed1-9b7a5b38ab92}" FolderPath="ITF_DecoratedElement\">
      <Declaration><![CDATA[METHOD GetITFGroupElement : ITF_BaseGroupElement
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetITFGroupElement	:= null;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetITFTopElement" Id="{20a46a89-89ab-043b-0602-3d56a5db1953}" FolderPath="ITF_DecoratedElement\">
      <Declaration><![CDATA[METHOD GetITFTopElement : ITF_BaseTopElement
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetITFTopElement		:= null;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="JoinTheLogger" Id="{c36b1ddd-e3f3-0707-2e1b-24a85c94c962}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		07.10.2025		TC 4024.66
	Dodpisywanie się do listy logera
*)
METHOD PUBLIC JoinTheLogger : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (THIS^.bIsValidCommonData) THEN
	IF (THIS^.refstCommonData.itfLogger.AddItem (itfItem := THIS^) > 0 ) THEN
		JoinTheLogger		:= TRUE;
	END_IF
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterAdd" Id="{0bbb0fac-f279-0b70-341a-ed0caf27ff48}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		26.03.2025		TC 4024.66
	Dodawanie parametru do elementu wykonawczego - metode wywołać przy pierwszym uruchomieniu programu by wgrać parametry do pamięci
	później używać tylko edycji lub przywracania wartości domyślnych
*)
METHOD PUBLIC ParameterAdd : BOOL
VAR_INPUT
	stParameter			: ST_BaseDescriptor;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (THIS^.stParameters.uiNumber + 1 <= TO_UINT (GVL_BaseConstans.MAX_NUMBER_OF_PARAMETERS)) THEN
	THIS^.stParameters.uiNumber 		:= THIS^.stParameters.uiNumber + 1;
	THIS^.stParameters.arrRow [THIS^.stParameters.uiNumber] 	:= stParameter;
	ParameterAdd				:= TRUE;
ELSE
	ParameterAdd				:= FALSE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterCorrectDefaultValue" Id="{ea5554ac-46a4-0090-2247-7c38cc711c17}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		26.03.2025		TC 4024.66
	Modyfikuje wartość domyślną w istniejącym parametrze - true jak się zmiana udała
*)
METHOD PUBLIC ParameterCorrectDefaultValue : BOOL
VAR_INPUT
	uiNumber			: UINT;
	rValue				: REAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (uiNumber > 0 AND uiNumber <= THIS^.stParameters.uiNumber) THEN
	IF (rValue >= THIS^.GetMinFrom (stIN := THIS^.stParameters.arrRow [uiNumber]) AND
		rValue <= THIS^.GetMaxFrom (stIN := THIS^.stParameters.arrRow [uiNumber])) THEN
		THIS^.SetDefaultValueIn (stIN := THIS^.stParameters.arrRow [uiNumber], rDefaultValue	:= rValue);
		ParameterCorrectDefaultValue		:= TRUE;
	ELSE
		ParameterCorrectDefaultValue		:= FALSE;
	END_IF
ELSE
	ParameterCorrectDefaultValue		:= FALSE;
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterCorrectName" Id="{cc4d8743-c51f-0c67-0f5d-3ed9379dd6b6}" FolderPath="Parameter\">
      <Declaration><![CDATA[METHOD PUBLIC ParameterCorrectName : BOOL
VAR_INPUT
	eLocation					: E_BaseLocation;
	uiNumberOfParameter			: UINT;
	wsDescription				: WSTRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.SetTextIn (
	stIN 			:= THIS^.stParameters.arrRow [uiNumberOfParameter],
	eLocation		:= eLocation,
	wsDescription	:= wsDescription
);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterCorrectValue" Id="{7c6db1a2-5e76-0e89-3719-a5d4c4278993}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		26.03.2025		TC 4024.66
	Modyfikuje wartość w istniejącym parametrze - true jak się zmiana udała
*)
METHOD PUBLIC ParameterCorrectValue : BOOL
VAR_INPUT
	uiNumber			: UINT;
	rValue				: REAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (uiNumber > 0 AND uiNumber <= THIS^.stParameters.uiNumber) THEN
	IF (rValue <= THIS^.GetMaxFrom (stIN := THIS^.stParameters.arrRow [uiNumber]) AND
		rValue >= THIS^.GetMinFrom (stIN := THIS^.stParameters.arrRow [uiNumber]) ) THEN
		THIS^.SetValueIn (
			stIN		:= THIS^.stParameters.arrRow [uiNumber],
			rValue		:= rValue
		);
		ParameterCorrectValue		:= TRUE;
	ELSE
		ParameterCorrectValue		:= FALSE;
	END_IF
ELSE
	ParameterCorrectValue		:= FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterCreateTable" Id="{5c2e0dd4-2d80-0a33-06c5-97040748aaef}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		31.03.2025		TC 4024.66
	Metoda wymazująca dotychczasowe dane z tablicy parametrów i przypisująca je od nowa
*)
METHOD PROTECTED ParameterCreateTable : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[MEMSET (
	destAddr			:= ADR (THIS^.stParameters.arrRow),
	fillByte			:= 0,
	n					:= SIZEOF (THIS^.stParameters.arrRow)
);
THIS^.stParameters.uiNumber	:= 0;

ParameterCreateTable := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterGet" Id="{d8c0108f-8a5a-0b99-2668-1679555db078}" FolderPath="Parameter\">
      <Declaration><![CDATA[METHOD PUBLIC ParameterGet : ST_BaseDescriptor
VAR_INPUT
	uiNumer				: UINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[ParameterGet			:= THIS^.stParameters.arrRow [uiNumer];
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterGetDefaultValue" Id="{988eb6f3-0732-0567-16cf-3de68eec0a9a}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		26.03.2025		TC 4024.66
	Zwraca wartosc domyslna
*)
METHOD PUBLIC ParameterGetDefaultValue : REAL
VAR_INPUT
	uiNumber				: UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (uiNumber > 0 AND uiNumber <= THIS^.stParameters.uiNumber) THEN
	ParameterGetDefaultValue			:= THIS^.GetDefaultValueFrom (stIN :=  THIS^.stParameters.arrRow [uiNumber]);
ELSE
	ParameterGetDefaultValue			:= 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterGetMax" Id="{5c28c07f-b14c-0760-37a9-6dacdce80dc2}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		26.03.2025		TC 4024.66
	Zwraca wartość MAX
*)
METHOD PUBLIC ParameterGetMax : REAL
VAR_INPUT
	uiNumber				: UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (uiNumber > 0 AND uiNumber <= THIS^.stParameters.uiNumber) THEN
	ParameterGetMax			:= THIS^.GetMaxFrom (stIN :=  THIS^.stParameters.arrRow [uiNumber]);
ELSE
	ParameterGetMax			:= 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterGetMin" Id="{89312f05-57f5-0184-360c-08b141ef26e1}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		26.03.2025		TC 4024.66
	Zwraca wartość MIN
*)
METHOD PUBLIC ParameterGetMin : REAL
VAR_INPUT
	uiNumber				: UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (uiNumber >= 0 AND uiNumber <= THIS^.stParameters.uiNumber) THEN
	ParameterGetMin			:= THIS^.GetMinFrom (stIN := THIS^.stParameters.arrRow [uiNumber]);
ELSE
	ParameterGetMin			:= 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterGetNumberOfAdded" Id="{74c11ec2-a556-0bd7-3162-91c4a03b75e5}" FolderPath="Parameter\">
      <Declaration><![CDATA[METHOD PUBLIC ParameterGetNumberOfAdded : UINT
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ParameterGetNumberOfAdded			:= THIS^.stParameters.uiNumber;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterGetUnit" Id="{bb9b2833-f611-0468-2fc2-8a7dc470b3d3}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		26.03.2025		TC 4024.66
	Zwraca jednostke
*)
METHOD PUBLIC ParameterGetUnit : STRING(6)
VAR_INPUT
	uiNumber				: uINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (uiNumber > 0 AND uiNumber <= THIS^.stParameters.uiNumber) THEN
	ParameterGetUnit			:= THIS^.GetUnitFrom (stIN :=  THIS^.stParameters.arrRow [uiNumber]);
ELSE
	ParameterGetUnit			:= '';
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterGetValue" Id="{bc97081f-e202-05b7-3cad-6b67ebf2409d}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		26.03.2025		TC 4024.66
	Zwraca wartość
*)
METHOD PUBLIC ParameterGetValue : REAL
VAR_INPUT
	uiNumber				: UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (uiNumber > 0 AND uiNumber <= THIS^.stParameters.uiNumber) THEN
	ParameterGetValue			:= THIS^.GetValueFrom (stIN := THIS^.stParameters.arrRow [uiNumber]);
ELSE
	ParameterGetValue			:= 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterModify" Id="{e578fbe4-e52c-0213-244a-972cbc85a1ef}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		26.03.2025		TC 4024.66
	Modyfikacja istniejącego parametra jako przypisanie nowego deskryptora
*)
METHOD PUBLIC ParameterModify : BOOL
VAR_INPUT
	uiNumber			: UINT;
	stParameter			: ST_BaseDescriptor;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (uiNumber > 0 AND uiNumber <= THIS^.stParameters.uiNumber) THEN
	THIS^.stParameters.arrRow [uiNumber]	:= 	stParameter;
	ParameterModify		:= TRUE;
ELSE
	ParameterModify		:= FALSE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterRestoreDefaultValue" Id="{139a3102-43d8-0081-0eb2-892be4ec2d31}" FolderPath="Parameter\">
      <Declaration><![CDATA[METHOD PUBLIC ParameterRestoreDefaultValue : BOOL
VAR_INPUT
	uiNumber		: UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (uiNumber > 0 AND uiNumber <= THIS^.stParameters.uiNumber) THEN
	THIS^.SetValueIn (stIN := THIS^.stParameters.arrRow [uiNumber], rValue := THIS^.GetDefaultValueFrom (stIN := THIS^.stParameters.arrRow [uiNumber]));
	ParameterRestoreDefaultValue	:= TRUE;
ELSE
	ParameterRestoreDefaultValue	:= FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterRestoreDefaultValueAll" Id="{0026bcd4-2ada-0a85-0709-2aa759eab8a8}" FolderPath="Parameter\">
      <Declaration><![CDATA[METHOD PROTECTED ParameterRestoreDefaultValueAll : BOOL
VAR_INPUT
END_VAR

VAR
	iI					: INT;
	bResult				: bool;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bResult					:= TRUE;
FOR iI := 1 TO TO_INT (THIS^.stParameters.uiNumber) DO 
	bResult				:= bResult AND THIS^.ParameterRestoreDefaultValue (uiNumber := TO_UINT (iI));
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParametersCreate" Id="{c946a0bf-5c4e-0c5c-0419-301bd87e62bd}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		04.05.2025		TC 4024.66
	Skasowanie i utworzenie tablicy parametrów od nowa 
*)
METHOD PUBLIC ParametersCreate : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.cmdParametersCreate.Execute ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ParameterSetToDefault" Id="{3020fada-bbb7-04bb-27d1-a0f7481f168e}" FolderPath="Parameter\">
      <Declaration><![CDATA[(*			
	EVERDE sp. z o.o.
	Rafał Osajda		04.05.2025		TC 4024.66
	Przywrocenie parametrow - in na false to tylko ten element true razem z podelementami
*)
METHOD PUBLIC ParameterSetToDefault : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.cmdParametersRestoreToDefault.Execute ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{c0bf4177-e97f-089b-34b4-b48fadeb74cc}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[METHOD PUBLIC Reset : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.cmdReset.Execute ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ResetGetBusy" Id="{3b84d943-6444-09e4-127e-9583cbeebff0}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[METHOD PUBLIC ResetGetBusy : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ResetGetBusy		:= THIS^.cmdReset.GetBusy ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ResetGetStatus" Id="{d861273e-29d7-0151-12ed-78079fcd5e28}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[METHOD PUBLIC ResetGetStatus : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ResetGetStatus			:= THIS^.cmdReset.GetStatus ();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ResetOperation" Id="{2d760c90-c43b-0af4-3d18-a1d7b05cea9c}" FolderPath="ITF_BaseComponent\">
      <Declaration><![CDATA[METHOD PROTECTED ResetOperation : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[

ResetOperation			:= FALSE;
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>