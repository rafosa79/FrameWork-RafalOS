<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_MachineControl" Id="{03d9ff00-a300-4d9d-a349-75e3318a4c90}" SpecialFunc="None">
    <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		04.11.2025		TC 4024.66
	Główny FB zarządzający pracą maszyny
*)
FUNCTION_BLOCK FB_MachineControl EXTENDS FB_BaseTopElement IMPLEMENTS ITF_MachineControlConfigure
VAR
	(*	Inicjalizacja		*)
	bInitializeCycle			: BOOL;
	bInitDone					: BOOL;
	(*	Konfiguracja		*)
	itfMachineConfigure			: ITF_MachineConfigure;		//moduł konfiguracji
	(*	TopElementu systemu	*)
	arrTopElements					: ARRAY [1..GVL_BaseConstans.MAX_NUMBER_OF_TOP_ELEMENT] OF ITF_BaseTopElement;
	uiNumberOfAddedTopElement		: UINT := 0;//przy dodawaniu element 0 to system 
	uiNumberOfAllAddedElements		: UINT;		//liczba elementow które są na maszynie 
	(*	Master komunikatora dla top elementów maszyny	*)
	fbMasterCommunicator			: FB_BaseCommunicator;
	(*	Zarządzenia stanami i trybami pracy	*)
	itfSaMController				: ITF_SaMController;
	(*	Stany i Tryby	*)
	arrRequestChangeState			: ARRAY [1..GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_STATES] OF ITF_RequiredChange;
	arrRequestChangeMode			: ARRAY [1..GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_MODES] OF ITF_RequiredChange;
	(*	Tablica informacyjna czy praca w stanie aktywnym zakończona	*)
	arrTopElementWorkFinishedInState	: ARRAY [1..GVL_SaMConstans.NUMBER_OF_STATES] OF BOOL;
	bTopElementStateWorkCompleted	: BOOL;	//praca w trybach aktywnych zakończona
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF (NOT THIS^.bInitializeCycle) THEN
	THIS^.bInitializeCycle		:= TRUE;
	THIS^.bInitDone				:= THIS^.InitializeCycle ();
ELSIF (THIS^.bInitDone) THEN
	THIS^.ExecuteCycle ();
END_IF
]]></ST>
    </Implementation>
    <Folder Name="ITF_MachineControlConfigure" Id="{973d3ff0-85f0-4bae-9395-cc62a4917833}" />
    <Folder Name="WiazanieModulow" Id="{3d8e3574-09c0-0f20-0429-cf1b0f8aec79}" />
    <Method Name="AddModulConfigure" Id="{e05e86ab-e65a-0460-123e-9f866e36dccc}" FolderPath="WiazanieModulow\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		29.10.2025		TC 4024.66
	Dodawanie intefejsu konfiguracji
*)
METHOD PUBLIC AddModulConfigure : BOOL
VAR_INPUT
	fbMachineConfigure			: ITF_MachineConfigure;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.itfMachineConfigure		:= fbMachineConfigure;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddModulSaM" Id="{fdfb6f32-ad29-05b9-1f5f-f81a0f2962ac}" FolderPath="WiazanieModulow\">
      <Declaration><![CDATA[METHOD PUBLIC AddModulSaM : BOOL
VAR_INPUT
	fbSaMController			: ITF_SaMController;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.itfSaMController		:= fbSaMController;
THIS^.AddControlElement (itfControlElement	:= fbSaMController);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddRequestModeChange" Id="{eee38abc-f39d-0ce2-0412-12f1dcddd8de}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[METHOD PUBLIC AddRequestModeChange : BOOL
VAR_INPUT
	iNumberOfRequest			: INT;
	itfRequest					: ITF_RequiredChange;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrRequestChangeMode [iNumberOfRequest]	:= itfRequest;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddRequestStateChange" Id="{3a316008-6053-0642-3d72-eda4def5bdc4}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[METHOD PUBLIC AddRequestStateChange : BOOL
VAR_INPUT
	iNumberOfRequest			: INT;
	itfRequest					: ITF_RequiredChange;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.arrRequestChangeState [iNumberOfRequest]	:= itfRequest;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddTopElementFromConfiguration" Id="{100f011a-3941-0f6f-14c0-c4dea046c178}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[(*			
	RafalOS
	Rafał Osajda		29.10.2025		TC 4024.66
	Metoda dodawania top elementu wywoływana z konfiguracji
*)
METHOD AddTopElementFromConfiguration : BOOL
VAR_INPUT
	fbTopElement		: ITF_BaseTopElement;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (THIS^.uiNumberOfAddedTopElement + 1 <= GVL_BaseConstans.MAX_NUMBER_OF_TOP_ELEMENT) THEN
	THIS^.uiNumberOfAddedTopElement							:= THIS^.uiNumberOfAddedTopElement + 1;
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement]	:= fbTopElement;
	
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkCommonData (stCommonData	:= THIS^.refstCommonData);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkSS1 (fbSS1	:= THIS^.itfSS1);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkSTO (fbSTO	:= THIS^.itfSTO);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkCommunicator (fbCommunicator	:= THIS^.fbMasterCommunicator);
	(*	Podpięcie systemu sterowania stanami i trybami	*)
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].LinkSAM (fbSAM	:= THIS^.itfSaMController);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].FirstCycle ();
	THIS^.uiNumberOfAllAddedElements 						:= THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].SetID (uiID := THIS^.uiNumberOfAllAddedElements + 1);
	THIS^.refstCommonData.itfLogger.SetNumerOfReportsIntheSystem (
			uiValue := THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].SetEventsIDs (
				uiNewID := THIS^.refstCommonData.itfLogger.GetNumerOfReportsIntheSystem ()
			)
	);
	THIS^.arrTopElements [THIS^.uiNumberOfAddedTopElement].SetCode (sCode := '0.0.0.0.0', sAdd := TO_STRING (THIS^.uiNumberOfAddedTopElement));
	
	AddTopElementFromConfiguration							:= TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteCycle" Id="{7066db40-c8ea-4d9b-9b50-7a0d826fe39e}">
      <Declaration><![CDATA[METHOD PRIVATE ExecuteCycle

VAR
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[	(*	Zarządzanie Stanami i Trybami	*)

THIS^.ExecuteCycle_SaM ();

	(*	Wywołanie metod obsługiwanych co cykl dla każdego elementu	*)

FOR iI := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
	THIS^.arrTopElements [iI].ExecuteEveryCycle_Test ();
	THIS^.arrTopElements [iI].ExecuteEveryCycle_Operation ();
	THIS^.arrTopElements [iI].ExecuteEveryCycle_Status ();
	THIS^.arrTopElements [iI].ExecuteEveryCycle_Error ();
	
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecuteCycle_SaM" Id="{111ca252-3465-079b-3f8c-a42b1dc9a298}">
      <Declaration><![CDATA[METHOD PRIVATE ExecuteCycle_SaM
VAR
	iI					: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
	(*	Zarządzanie stanami i trybami pracy 	*)

	(*	Testowanie czy jest rządzanie zmiany trybu	*)
FOR iI := 1 TO GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_MODES DO 
	IF (THIS^.arrRequestChangeMode [iI] <> 0) THEN
		IF (THIS^.arrRequestChangeMode [iI].Testing ()) THEN
			THIS^.itfSaMController.Execute (iNumberOfCommand := iI, eChangeCategory := E_SaMChangeCategory.eMode);
		END_IF
	END_IF
END_FOR	
	
	(*	Testowanie czy jest rządanie zmiany stany	*)
FOR iI := 1 TO GVL_SaMConstans.NUMBER_OF_COMMAND_CHANGE_STATES DO 
	IF (THIS^.arrRequestChangeState [iI] <> 0) THEN
		IF (THIS^.arrRequestChangeState [iI].Testing ()) THEN
			THIS^.itfSaMController.Execute (iNumberOfCommand := iI, eChangeCategory := E_SaMChangeCategory.eState);
		END_IF
	END_IF
END_FOR

	(*	Obsługa stanów aktywnych - ing	*)
THIS^.bTopElementStateWorkCompleted						:= SEL (THIS^.itfSaMController.GetState () <> E_BaseMachineState.Undefined,FALSE,THIS^.itfSaMController.IsStateTypeActive ()); //informacja czy jesteśny w stanie aktywnym 
IF (NOT THIS^.itfSaMController.IsChangeModeBusy () AND NOT THIS^.itfSaMController.IsChangeStateBusy ()) THEN
	IF (THIS^.itfSaMController.GetState () <> E_BaseMachineState.Undefined) THEN
		FOR iI := 1 TO TO_INT (THIS^.uiNumberOfAddedTopElement) DO
			THIS^.arrTopElementWorkFinishedInState [iI]	:= THIS^.itfSaMController.ExecuteTopElementOperationsForCurrentState (itfTopElement := THIS^.arrTopElements [iI]);
			THIS^.bTopElementStateWorkCompleted			:= THIS^.bTopElementStateWorkCompleted AND THIS^.arrTopElementWorkFinishedInState [iI];
		END_FOR
	END_IF
END_IF
(*	OPeracja zakonczona wyczysc tablice dla kolejnej zmiany stanow i konczenia pracy	*)
IF (THIS^.bTopElementStateWorkCompleted) THEN
	MEMSET (
		destAddr							:= ADR (THIS^.arrTopElementWorkFinishedInState),
		fillByte							:= 0,
		n									:= SIZEOF (THIS^.arrTopElementWorkFinishedInState)
	);
	(*	Zmiana stanu na następny wyjście ze stanu aktywnego	*)
	THIS^.itfSaMController.ExitActivState ();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="InitializeCycle" Id="{f127c58d-f380-4c5b-91fc-37aed1ddc70f}">
      <Declaration><![CDATA[METHOD PRIVATE InitializeCycle : BOOL
VAR
	bResult					: BOOL;
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bResult				:= TRUE;

bResult				:= bResult AND THIS^.itfMachineConfigure.Configure (fbMachineControl := THIS^);

//FOR iI := 0 TO THIS^.iNumberOfAddedTopElement DO
//	bResult				:= bResult AND THIS^.arrTopElements [iI].FirstCycle ();
//END_FOR



InitializeCycle		:= bResult;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetStartMode" Id="{d33b08a8-5ec6-0245-01ba-557e6d74ea6b}" FolderPath="ITF_MachineControlConfigure\">
      <Declaration><![CDATA[METHOD PUBLIC SetStartMode
VAR_INPUT
	eStartMode				: E_BaseMachineMode;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.itfSaMController.Execute (iNumberOfCommand := eStartMode, eChangeCategory := E_SaMChangeCategory.eMode);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>