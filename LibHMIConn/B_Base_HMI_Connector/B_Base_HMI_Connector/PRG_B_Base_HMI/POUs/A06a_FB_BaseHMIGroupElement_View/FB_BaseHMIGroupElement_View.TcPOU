<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_BaseHMIGroupElement_View" Id="{3f198324-9925-0fb3-39d4-b6620e2a327f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_BaseHMIGroupElement_View EXTENDS FB_BaseHMIControlElement_View IMPLEMENTS ITF_BaseHMIGroupElement_View
VAR
	itfBaseGroupElementConnector		: ITF_BaseGroupElement_HMI_Connector;
	refstBaseGroupElementDataHMI		: REFERENCE TO ST_BaseHMIGroupElement_View;
	(*	lista elementów składających się na ten element w elemencie docelowym dodać itf dla konkretnych elementów	*)
	arrControlElement					: ARRAY [1..GVL_BaseConstans.MAX_ITEM_PER_ELEMENT] OF ITF_BaseHMIControlElement_View;
	uiNumberOfControlElement			: UINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="ITF_BaseLoggerElement_HMI" Id="{43a58e74-3038-07b9-3465-541bb380e3c9}" />
    <Folder Name="WiazanieZmiennych" Id="{4dfffb3d-2831-0778-0d83-d737d6645326}" />
    <Method Name="AddControlElement" Id="{76a032d3-46d3-05c1-2da2-40ea71aff009}">
      <Declaration><![CDATA[METHOD PROTECTED AddControlElement : BOOL
VAR_INPUT
	itfControlElementHMI		: ITF_BaseHMIControlElement_View;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (THIS^.uiNumberOfControlElement + 1 <= GVL_BaseConstans.MAX_ITEM_PER_ELEMENT) THEN
	THIS^.uiNumberOfControlElement		:= THIS^.uiNumberOfControlElement + 1;
	THIS^.arrControlElement [THIS^.uiNumberOfControlElement]	:= itfControlElementHMI;
	AddControlElement			:= TRUE;
ELSE
	;//zwrocenie zbyt dużo komponentow
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsLinkedReferences" Id="{10415db9-e9ae-0eda-0306-424e71472c92}" FolderPath="WiazanieZmiennych\">
      <Declaration><![CDATA[METHOD PUBLIC IsLinkedReferences : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (SUPER^.IsLinkedReferences () AND __ISVALIDREF (THIS^.refstBaseGroupElementDataHMI)) THEN
	IsLinkedReferences			:= TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsValidCommonData" Id="{5d939d1c-53e8-00ed-38cd-40de2c94258f}" FolderPath="WiazanieZmiennych\">
      <Declaration><![CDATA[METHOD PUBLIC IsValidCommonData : BOOL
VAR_INPUT
END_VAR

VAR
	bResult: BOOL;
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bResult						:= SUPER^.IsValidCommonData ();

FOR iI := 1 TO TO_INT (THIS^.uiNumberOfControlElement) DO
	bResult					:= bResult AND THIS^.arrControlElement [iI].IsValidCommonData ();
END_FOR

IsValidCommonData			:= bResult;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="JoinTheList" Id="{8b5de760-b6b3-434e-b4d6-a0aa0ea8e4bb}" FolderPath="ITF_BaseLoggerElement_HMI\">
      <Declaration><![CDATA[METHOD PUBLIC JoinTheList : INT
VAR_INPUT
	refstList				: REFERENCE TO ST_ListOfElement_HMI;
	iNumberOfParent			: INT; 	//numer rodzica na liście
END_VAR
VAR
	iI						: INT;
	itmpNumberOfParent			: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[itmpNumberOfParent		:= SUPER^.JoinTheList (refstList := refstList, iNumberOfParent := iNumberOfParent);

FOR iI := 1 TO TO_INT (THIS^.uiNumberOfControlElement) DO
	THIS^.arrControlElement [iI].JoinTheList (refstList := refstList, iNumberOfParent := itmpNumberOfParent);
END_FOR

JoinTheList	:= itmpNumberOfParent;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="LinkCommonData" Id="{22a632e2-05dc-0cea-3d80-261c3ce0e978}" FolderPath="WiazanieZmiennych\">
      <Declaration><![CDATA[METHOD PUBLIC LinkCommonData : BOOL
VAR_INPUT
	stCommonData				: REFERENCE TO ST_BaseCommonData;
END_VAR
VAR
	bResult						: BOOL;
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bResult							:= SUPER^.LinkCommonData (stCommonData := stCommonData);

FOR iI := 1 TO TO_INT (THIS^.uiNumberOfControlElement) DO
	bResult := bResult AND THIS^.arrControlElement [iI].LinkCommonData (stCommonData := stCommonData);
END_FOR

LinkCommonData					:= bResult;
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>