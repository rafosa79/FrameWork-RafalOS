<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_BaseHMIControlElement_View" Id="{f7e14e4d-7841-0cfb-1cf8-aac3b29affb7}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_BaseHMIControlElement_View EXTENDS FB_BaseHMIComponent_View IMPLEMENTS ITF_BaseHMIControlElement_View
VAR
	itfBaseControlElementConnector		: ITF_BaseControlElement_HMI_Connector;
	refstBaseControlElementDataHMI		: REFERENCE TO ST_BaseHMIControlElement_View;
	(*	lista componentów składających się na ten element w elemencie docelowym dodać itf dla konkretnych elementów	*)
	arrComponent						: ARRAY  [1..GVL_BaseConstans.MAX_ITEM_PER_ELEMENT] OF ITF_BaseHMIComponent_View;
	uiNumberOfComponents				: UINT;

END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="ITF_BaseLoggerElement_HMI" Id="{5a0c3962-8b57-0c26-2752-f9c63b31ee4d}" />
    <Folder Name="WiazanieZmiennych" Id="{d59cbb33-4872-0c07-2f02-56b2cb96f021}" />
    <Method Name="AddComponent" Id="{d792cffc-9654-05bf-1253-a43b4835c74a}">
      <Declaration><![CDATA[METHOD PROTECTED AddComponent : BOOL
VAR_INPUT
	itfComponentHMI				: ITF_BaseHMIComponent_View;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (THIS^.uiNumberOfComponents + 1 <= GVL_BaseConstans.MAX_ITEM_PER_ELEMENT) THEN
	THIS^.uiNumberOfComponents		:= THIS^.uiNumberOfComponents + 1;
	THIS^.arrComponent [THIS^.uiNumberOfComponents]	:= itfComponentHMI;
	AddComponent				:= TRUE;
ELSE
	;//zwrocenie zbyt dużo komponentow
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsLinkedReferences" Id="{dc1a6837-3c44-0bee-3b81-e102a455fb15}" FolderPath="WiazanieZmiennych\">
      <Declaration><![CDATA[METHOD PUBLIC IsLinkedReferences : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (SUPER^.IsLinkedReferences () AND __ISVALIDREF (THIS^.refstBaseControlElementDataHMI)) THEN
	IsLinkedReferences			:= TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsValidCommonData" Id="{a445a680-745f-0343-03ff-c574404161a6}" FolderPath="WiazanieZmiennych\">
      <Declaration><![CDATA[METHOD PUBLIC IsValidCommonData : BOOL
VAR_INPUT
END_VAR

VAR
	bResult: BOOL;
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bResult						:= SUPER^.IsValidCommonData ();

FOR iI := 1 TO TO_INT (THIS^.uiNumberOfComponents) DO
	bResult					:= bResult AND THIS^.arrComponent [iI].IsValidCommonData ();
END_FOR

IsValidCommonData			:= bResult;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="JoinTheList" Id="{1ae6630f-2586-4a9e-99c0-0fa80e347fc1}" FolderPath="ITF_BaseLoggerElement_HMI\">
      <Declaration><![CDATA[METHOD PUBLIC JoinTheList : INT
VAR_INPUT
	refstList				: REFERENCE TO ST_ListOfElement_HMI;
	iNumberOfParent			: INT; 	//numer rodzica na liście
END_VAR
VAR
	iI						: INT;
	itmpNumberOfParent		: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[itmpNumberOfParent	:= SUPER^.JoinTheList (refstList := refstList, iNumberOfParent := iNumberOfParent);

FOR iI := 1 TO TO_INT (THIS^.uiNumberOfComponents) DO
	THIS^.arrComponent [iI].JoinTheList (refstList := refstList, iNumberOfParent := itmpNumberOfParent);
END_FOR

JoinTheList	:= itmpNumberOfParent;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="LinkCommonData" Id="{d5df75cc-0805-00ee-3cc4-35bd7d2ef134}" FolderPath="WiazanieZmiennych\">
      <Declaration><![CDATA[METHOD PUBLIC LinkCommonData : BOOL
VAR_INPUT
	stCommonData				: REFERENCE TO ST_BaseCommonData;
END_VAR
VAR
	bResult						: BOOL;
	iI: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bResult							:= SUPER^.LinkCommonData (stCommonData := stCommonData);

FOR iI := 1 TO TO_INT (THIS^.uiNumberOfComponents) DO
	bResult := bResult AND THIS^.arrComponent [iI].LinkCommonData (stCommonData := stCommonData);
END_FOR

LinkCommonData					:= bResult;
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>